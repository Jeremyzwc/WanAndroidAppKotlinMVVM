plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}
apply plugin: 'kotlin-android'
apply from: "${rootDir}/common.gradle"
apply from: "${rootDir}/multi_channel.gradle"
apply plugin: 'me.ele.mess'

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.qisan.wanandroid"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode getLocalVersionCode()
        versionName "1.0.0"

        buildConfigField("boolean", "IS_TEST_EVN", rootProject.ext.IS_TEST_EVN)
        buildConfigField("boolean", "IS_PUBLISH", rootProject.ext.IS_PUBLISH)

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters = []
            abiFilters.addAll(rootProject.ext.abiFilters)
        }

    }

    signingConfigs {
        release {
            storeFile file(rootProject.ext.wanAndroidConfig.releaseKeystorePath)
            keyAlias rootProject.ext.wanAndroidConfig.keyAlias
            storePassword rootProject.ext.wanAndroidConfig.storePassword
            keyPassword rootProject.ext.wanAndroidConfig.keyPassword
        }
    }

    productFlavors.all { flavor ->
        flavor.dimension = "channel"
        //这里可以根据渠道配置不同的信息 有时候可能存在不同的签名的情况，比如官网的包和googlePlay上架的包不一样
        if(name == 'google' ){
            flavor.applicationId = "com.qisan.wanandroid"
            flavor.signingConfig = signingConfigs.release
            flavor.manifestPlaceholders = [
                    APP_NAME     : "WanAndroid",
                    CHANNEL_NAME : name,
                    FILE_PROVIDER: "com.qisan.wanandroid.fileprovider"]
        }else {
            flavor.applicationId = "com.qisan.wanandroid"
            flavor.signingConfig = signingConfigs.release
            flavor.manifestPlaceholders = [
                    APP_NAME     : "玩安卓",
                    CHANNEL_NAME : name,
                    FILE_PROVIDER: "com.qisan.wanandroid.fileprovider"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }

    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->

//            output.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/releaseApks")

            if (variant.buildType.name == 'release') {
                output.outputFileName =
                        "WanAndroid_${variant.flavorName}_${buildType.name}_v${variant.versionCode}_n${variant.versionName}_" + getNetworkType() + "_" + getPublish() + "_" + releaseTime() + ".apk"
            } else {
                // debug
                output.outputFileName = "${project.getName()}_${variant.flavorName}_${buildType.name}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //Room官方建议：在`android`块中添加以下代码块，以从软件包中排除原子函数模块并防止出现警告
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //协程
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"

    //导航组件
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.2"
    //适配
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //打印http请求日志
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"
    //下拉刷新
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"

    //banner图控件
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.bingoogolapple:BGABanner-Android:3.0.1'

    implementation project(':mvvmArch')

    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

}
//
//tasks.whenTaskAdded {task ->
//    if (task.name.contains("ApkListingFileRedirect")) {
//        task.enabled = false
//    }
//}
