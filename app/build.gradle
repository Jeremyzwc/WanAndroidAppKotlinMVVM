plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}
apply plugin: 'kotlin-android'
apply from: "${rootDir}/common.gradle"

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.qisan.wanandroid"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode getLocalVersionCode()
        versionName "1.0"

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters = []
            abiFilters.addAll(rootProject.ext.abiFilters)
        }

        flavorDimensions "versionCode"
    }

    flavorDimensions "channel"

    productFlavors {
        google {
            dimension "channel"
            manifestPlaceholders = [CHANNEL_NAME: "google"]
        }

        huawei {
            dimension "channel"
            manifestPlaceholders = [CHANNEL_NAME: "huawei"]
        }
    }

    signingConfigs {
        release {
            storeFile file(rootProject.ext.wanAndroidConfig.releaseKeystorePath)
            keyAlias rootProject.ext.wanAndroidConfig.keyAlias
            storePassword rootProject.ext.wanAndroidConfig.storePassword
            keyPassword rootProject.ext.wanAndroidConfig.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }

    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name == 'release') {
                output.outputFileName =
                        "WanAndroid_${variant.flavorName}_${buildType.name}_v${variant.versionCode}_n${variant.versionName}_" + getNetworkType() + "_" + getPublish() + "_" + releaseTime() + ".apk"

            } else {
                // debug
                output.outputFileName = "${project.getName()}_${variant.flavorName}_${buildType.name}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures{
        viewBinding true
    }
}

dependencies {

    implementation "androidx.core:core-ktx:1.7.0"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //协程
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"
    //LiveData处理activity销毁重建的数据倒灌问题，可以被多个观察者订阅
    implementation 'com.kunminx.arch:unpeek-livedata:7.4.0'
    //导航组件
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.2"
    //适配
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'

    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //打印http请求日志
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"
    //下拉刷新
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"

    implementation "com.github.bumptech.glide:glide:4.13.2"
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.1'
    //banner图控件
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.bingoogolapple:BGABanner-Android:3.0.1'

    implementation 'q.rorbin:badgeview:1.1.2'
    implementation 'com.hyman:flowlayout-lib:1.1.2'
    //基于LiveData代替EventBus的消息总线库，具有生命周期感知
    implementation 'io.github.jeremyliao:live-event-bus-x:1.8.0'

    implementation 'com.just.agentweb:agentweb-androidx:4.1.4'

    // leakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.7"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.1"
}